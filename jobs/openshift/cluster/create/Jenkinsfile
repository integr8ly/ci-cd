#!groovy

def clusterOptions = [:]

def verifyClusterOptions(clusterOptions) {
    if (!clusterOptions.name || !clusterOptions.awsRegion || !clusterOptions.awsAccountName) {
        choiceAWSAccountName = new ChoiceParameterDefinition('awsAccountName', ['fheng'] as String[], 'AWS Account Name')
        def userInput = input message: 'Cluster Options', parameters: [
                string(defaultValue: (clusterOptions.name ?: ''), description: 'Cluster name', name: 'clusterName'),
                string(defaultValue: (clusterOptions.awsRegion ?: 'eu-west-2'), description: 'AWS Region', name: 'awsRegion'),
                choiceAWSAccountName
        ]
        clusterOptions.name = userInput.clusterName
        clusterOptions.awsRegion = userInput.awsRegion
        clusterOptions.awsAccountName = userInput.awsAccountName
        verifyClusterOptions(clusterOptions)
    }
}

stage("Cluster Options") {
    clusterOptions.name = params.clusterName
    clusterOptions.awsRegion = params.awsRegion
    clusterOptions.awsAccountName = params.awsAccountName
    verifyClusterOptions(clusterOptions)
    println "Cluster Options: ${clusterOptions}"
    currentBuild.displayName = "${currentBuild.displayName} ${clusterOptions.name}"
    currentBuild.description = "clusterName: ${clusterOptions.name}\nawsRegion: ${clusterOptions.awsRegion}"
    clusterOptions.nodes = [
        'master': [
            'instance_type': "${master_instance_type}",
            'group_size': "${master_group_size}"
        ],
        'compute': [
            'instance_type': "${compute_instance_type}",
            'group_size': "${compute_group_size}"
        ],
        'infra': [
            'instance_type': "${infra_instance_type}",
            'group_size': "${infra_group_size}"
        ]
    ]
}

node('cirhos_rhel7') {
    cleanWs()
    stage('Create OpenShift Cluster') {
        println("Create OpenShift Cluster")
        if (params.dryRun) {
            println("Would call ansibleTower with: oo_clusterid: ${clusterOptions.name}, oo_sublocation: ${clusterOptions.awsRegion}")
        } else {
            def extraVars = [
                "tower_environment: dev",
                "oo_clusterid: ${clusterOptions.name}",
                "oo_sublocation: ${clusterOptions.awsRegion}",
                "cluster_credential_bundle_aws_name: ${clusterOptions.awsAccountName}",
                "survey_provision_logging: 'false'",
                "master_instance_size: ${clusterOptions.nodes.master.instance_type}",
                "compute_instance_size: ${clusterOptions.nodes.compute.instance_type}",
                "infra_instance_size: ${clusterOptions.nodes.infra.instance_type}",
                "openshift_aws_master_group_desired_size: ${clusterOptions.nodes.master.group_size}",
                "openshift_aws_compute_group_desired_size: ${clusterOptions.nodes.compute.group_size}",
                "openshift_aws_infra_group_desired_size: ${clusterOptions.nodes.infra.group_size}"
            ]
            
            if (clusterOptions.nodes.compute.group_size == '1') {
                extraVars << 'openshift_aws_compute_group_min_size: 1'
            }

            if (clusterOptions.nodes.infra.group_size == '1') {
                extraVars << 'openshift_aws_infra_group_min_size: 1'
            }

            def parsedVars = extraVars.join("\n")
            println "---VARS BEGIN---\n${parsedVars}\n---VARS END---"

            wrap([$class: 'AnsiColorBuildWrapper', colorMapName: 'xterm']) {
                ansibleTower(
                        towerServer: 'Dev Tower',
                        jobTemplate: 'Provision Cluster',
                        templateType: 'workflow',
                        importTowerLogs: true,
                        removeColor: false,
                        verbose: true,
                        extraVars: parsedVars
                )
            }
        }
    }
}
