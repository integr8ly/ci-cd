---

- job:
    name: ocp4-install
    project-type: pipeline
    description: "Installs RHMI on your OpenShift 4 RHPDS or openshift-install cluster (with configuration stored here: https://gitlab.cee.redhat.com/integreatly-qe/rhmi-jenkins/tree/master/clusters"
    sandbox: false
    concurrent: true
    parameters:
        - string:
            name: CLUSTER_NAME
            description: "City or customer for RHPDS cluster, for example qe-ocp4-[hash] or cluster name specified when creating it using openshift-install"
        - string:
            name: CLUSTER_DOMAIN
            default: 'open.redhat.com'
            description: "Cluster domain - open.redhat.com or example.opentlc.com for RHPDS clusters and intlyqe.com for clusters created using openshift-install"
        - bool:
            name: PDS_CLUSTER
            default: true
            description: "Indicates whether cluster was provisioned using RHPDS or openshift-install"
        - bool:
            name: SELF_SIGNED_CERTS
            default: true
            description: "Indicates whether cluster uses self signed certificates or not"
        - string:
            name: ADMIN_USERNAME
            default: 'opentlc-mgr'
            description: "Admin username for the OCP4 cluster"
        - string:
            name: ADMIN_PASSWORD
            default: ''
            description: "Admin password for OS4 cluster"
        - string:
            name: INTEGREATLY_OPERATOR_REPOSITORY
            default: 'https://github.com/integr8ly/integreatly-operator.git'
            description: "Repository of the Integreatly Operator"
        - string:
            name: INTEGREATLY_OPERATOR_BRANCH
            default: 'master'
            description: "Branch of the Integreatly Operator repository"
        - string:
            name: REGISTRY_NAMESPACE
            default: 'integreatly'
            description: "Registry name in quay.io"
        - choice:
            name: INSTALLATION_TYPE
            description: "Installation type"
            choices:
              - workshop
              - managed
        - string:
            name: NAMESPACE_PREFIX
            description: "Prefix of Integreatly Operator namespace, defauls to `BUILD_NUMBER`"
        - bool:
            name: CLEANUP_BEFOREHAND
            default: true
            description: "Indicates whether to cleanup before execution"
        - bool:
            name: CLEANUP_AFTERWARD
            default: true
            description: "Indicates whether to cleanup after execution"
        - string:
            name: NAMESPACE_CLEANUP_BEFOREHAND
            description: "Namespace to be removed before the Integreatly Operator deploy. Can be left empty"
        - string:
            name: GH_CLIENT_ID
            description: "GitHub Client ID for OAuth Apps, required for some of the walkthroughs"
        - string:
            name: GH_CLIENT_SECRET
            description: "GitHub Client Secret for OAuth Apps, required for some of the walkthroughs"
        - string:
            name: TEST_SUITES_REPOSITORY
            default: 'https://gitlab.cee.redhat.com/integreatly-qe/integreatly-qe.git'
            description: "Repository of Integreatly test suites, YAML templates are stored there"
        - string:
            name: TEST_SUITES_BRANCH
            default: 'development'
            description: "Branch of Integreatly test suites repository"
        - string:
            name: RECIPIENTS
            default: 'integreatly-qe@redhat.com'
            description: "Whitespace- or comma-separated list of recipient addresses"
    dsl: |
        #!groovy
        Boolean cleanupBeforehand = params.CLEANUP_BEFOREHAND
        Boolean cleanupAfterward = params.CLEANUP_AFTERWARD
        Boolean selfSignedCerts = params.SELF_SIGNED_CERTS
        Boolean isPds = params.PDS_CLUSTER

        String registryNamespace = 'integreatly'
        if (params.REGISTRY_NAMESPACE) {
            registryNamespace = params.REGISTRY_NAMESPACE
        }

        String clusterSuffix = isPds ? "cluster-${CLUSTER_NAME}.${CLUSTER_NAME}.${CLUSTER_DOMAIN}" : "${CLUSTER_NAME}.${CLUSTER_DOMAIN}"
        
        String clusterAPI = "https://api.${clusterSuffix}:6443"

        String adminPassword = params.ADMIN_PASSWORD

        // by default build number is used as namespacePrefix to prevent clashes with previous Integreatly Operator deployments
        String namespacePrefix = BUILD_NUMBER
        if (NAMESPACE_PREFIX) {
            namespacePrefix = NAMESPACE_PREFIX
        }
        String namespaceName = namespacePrefix + '-integreatly-operator'

        String noConfigFilesError = "[ERROR] No configuration files found for ${CLUSTER_NAME}"
        String expectedOcCommandOutput = ""
        def err = null

        node('cirhos_rhel7') {
            timeout(60) { ansiColor('gnome-terminal') { timestamps {
                try {
                    currentBuild.displayName = "${currentBuild.displayName} ${CLUSTER_NAME}"

                    stage('Login to OpenShift 4 cluster') {
                        if (!isPds && adminPassword.toString().length() == 0) {
                            String loginCredentialDirectory = "clusters/${CLUSTER_NAME}/auth/kubeadmin-password"
                            dir("rhmi-jenkins") {
                                git branch: "master", url: "https://gitlab.cee.redhat.com/integreatly-qe/rhmi-jenkins.git"
                                def dirExists = fileExists(loginCredentialDirectory)
                                if (!dirExists) {
                                    error noConfigFilesError
                                }
                                adminPassword = sh (
                                    script: "cat ${loginCredentialDirectory}",
                                    returnStdout: true
                                ).trim()
                            }
                        }
                        sh "oc login ${clusterAPI} --insecure-skip-tls-verify=true -u ${ADMIN_USERNAME} -p ${adminPassword}"
                    } // stage
                    
                    stage('Cleanup Beforehand') {
                        if (cleanupBeforehand) {
                            cleanup(clusterAPI, NAMESPACE_CLEANUP_BEFOREHAND)
                        }
                    } // stage

                    stage('Clone the Integreatly Operator Repo') {
                        dir('integreatly-operator') {
                            checkout([
                                $class: 'GitSCM',
                                branches: [[name: INTEGREATLY_OPERATOR_BRANCH]],
                                doGenerateSubmoduleConfigurations: false,
                                extensions: [],
                                userRemoteConfigs: [[url: INTEGREATLY_OPERATOR_REPOSITORY]]
                            ])

                            sh "git clean -xdf"
                        } // dir
                    } // stage

                    stage ('Add Operator to the Cluster') {
                        withCredentials([string(credentialsId: 'qe-poc-aws-s3-key-id', variable: 'AWS_S3_KEY_ID')]) {
                            withCredentials([string(credentialsId: 'qe-poc-aws-s3-secret', variable: 'AWS_S3_SECRET')]) {
                                sh(
                                    returnStdout: false,
                                    script: """
                                        wget https://raw.githubusercontent.com/integr8ly/manifests/master/operator-source.yml
                                        sed -i 's@registryNamespace: integreatly@registryNamespace: ${registryNamespace}@' ./operator-source.yml
                                        oc create -f ./operator-source.yml

                                        oc create namespace ${namespaceName}

                                        oc process -f ./integreatly-operator/deploy/s3-secrets.yaml -p INSTALLATION_NAMESPACE=${namespaceName} -p AWS_ACCESS_KEY_ID=${AWS_S3_KEY_ID} -p AWS_SECRET_ACCESS_KEY=${AWS_S3_SECRET} -p AWS_BUCKET=qe-ocp4-integreatly-operator -p AWS_REGION=eu-west-1 | oc apply -f -
                                    """
                                )
                            } //withCredentials
                        } // withCredentials
                    } // stage

                    stage('Clone the Test Suite repo') {
                        dir('integreatly-qe') {
                            checkout([
                                $class: 'GitSCM',
                                branches: [[name: TEST_SUITES_BRANCH]],
                                doGenerateSubmoduleConfigurations: false,
                                extensions: [],
                                userRemoteConfigs: [[url: TEST_SUITES_REPOSITORY]]
                            ])

                            sh "git clean -xdf"
                        } // dir
                    } // stage

                    stage ('Install the Operator') {
                        sh(
                            returnStdout: false,
                            script: """
                                sed -i s@INTEGREATLY-OPERATOR_NAMESPACE@${namespaceName}@g ./integreatly-qe/resources/integreatly-operator/catalogsourceconfig-cr.yaml
                                sed -i s@INTEGREATLY-OPERATOR_NAMESPACE@${namespaceName}@g ./integreatly-qe/resources/integreatly-operator/operatorgroup-cr.yaml
                                sed -i s@INTEGREATLY-OPERATOR_NAMESPACE@${namespaceName}@g ./integreatly-qe/resources/integreatly-operator/subscription-cr.yaml

                                oc create -f ./integreatly-qe/resources/integreatly-operator/catalogsourceconfig-cr.yaml -n openshift-marketplace
                                oc create -f ./integreatly-qe/resources/integreatly-operator/operatorgroup-cr.yaml -n ${namespaceName}
                                oc create -f ./integreatly-qe/resources/integreatly-operator/subscription-cr.yaml -n ${namespaceName}
                            """
                        )
                    } // stage

                    stage ('Create GitHub secret') {
                        sh "oc create secret generic github-oauth-secret --from-literal=clientId=${GH_CLIENT_ID} --from-literal=secret=${GH_CLIENT_SECRET} -n ${namespaceName}"
                    } // stage

                    stage ('Create Installation CR') {

                        if (!selfSignedCerts) {
                            sh(
                                returnStdout: false,
                                script: '''
                                    sed -i "s@selfSignedCerts: true@selfSignedCerts: false@" ./integreatly-operator/deploy/crds/examples/installation.cr.yaml
                                '''
                            )
                        }

                        sh(
                            returnStdout: false,
                            script: """
                                sed -i "s@http://master.example.com@https://console-openshift-console.apps.${clusterSuffix}@" ./integreatly-operator/deploy/crds/examples/installation.cr.yaml
                                sed -i "s@example.com@apps.${clusterSuffix}@" ./integreatly-operator/deploy/crds/examples/installation.cr.yaml
                                sed -i "s@integreatly-@${namespacePrefix}-integreatly-@" ./integreatly-operator/deploy/crds/examples/installation.cr.yaml
                                sed -i "s@type: workshop@type: ${INSTALLATION_TYPE}@" ./integreatly-operator/deploy/crds/examples/installation.cr.yaml
                            """
                        )
                        sleep time: 60, unit: 'SECONDS'
                        String getOperatorReplicasCommand = "oc get deployments integreatly-operator -n ${namespaceName} --output=json -o jsonpath='{.status.availableReplicas}'"
                        expectedOcCommandOutput = getInstallationStatus(getOperatorReplicasCommand)
                        while (!expectedOcCommandOutput.contains("1")) {
                            println "Operator not ready yet"
                            sleep time: 5, unit: 'SECONDS'
                            expectedOcCommandOutput = getInstallationStatus(getOperatorReplicasCommand)
                        }       

                        sh "oc create -f ./integreatly-operator/deploy/crds/examples/installation.cr.yaml -n ${namespaceName}"
                    } // stage

                    stage ('Verify Installation Has Finished') {
                        String getAuthStatusCommand = "oc get Installation example-installation -n ${namespaceName} --output=json -o jsonpath='{.status.stages.authentication.phase}'"
                        expectedOcCommandOutput = getInstallationStatus(getAuthStatusCommand)
                        while (!expectedOcCommandOutput.contains("completed")) {
                            println "Authentication stage has not completed yet"
                            sleep time: 20, unit: 'SECONDS'
                            expectedOcCommandOutput = getInstallationStatus(getAuthStatusCommand)
                        }  

                        String getInstallationStatusCommand = "oc get Installation example-installation -n ${namespaceName} --output=json -o jsonpath='{.status.stages.products.phase}'"
                        expectedOcCommandOutput = getInstallationStatus(getInstallationStatusCommand)
                        while (!expectedOcCommandOutput.contains("completed")) {
                            println "Installation has not completed yet"
                            sleep time: 20, unit: 'SECONDS'
                            expectedOcCommandOutput = getInstallationStatus(getInstallationStatusCommand)
                        }
                    } // stage

                    stage ('Install dedicated-admin') {
                        if (isPds) {
                            sh "oc login ${clusterAPI} --insecure-skip-tls-verify=true -u ${ADMIN_USERNAME} -p ${adminPassword}"
                            dir("integreatly-operator") {
                                git branch: "${INTEGREATLY_OPERATOR_BRANCH}", url: "${INTEGREATLY_OPERATOR_REPOSITORY}"
                                sh "./scripts/dedicated-setup.sh"
                            }
                        }
                    } // stage
                } catch (caughtError){
                    currentBuild.result = 'FAILURE'
                    err = caughtError
                    println caughtError
                } finally {
                    notifyBuild(currentBuild.result, err)
                    
                    // it is possible that oc login will not work on openshift-install cluster - we don't want to do cleanup if that is the case
                    if (cleanupAfterward && !err.toString().contains(noConfigFilesError)) {
                        cleanup(clusterAPI, namespaceName)
                    }
                }
            }}} // timeout, ansiColor, timestamps
        } // node

        def getInstallationStatus(command) {
            String ocStatus = sh (
                script: "oc status",
                returnStdout: true
            ).trim()
            if (!ocStatus.contains("${CLUSTER_NAME}") || !ocStatus.contains("${CLUSTER_DOMAIN}")) {
                println "kubeconfig might have been changed. Trying to login again..."
                sh "oc login ${clusterAPI} --insecure-skip-tls-verify=true -u ${ADMIN_USERNAME} -p ${adminPassword}"
            }
            String ocCommandOutput = sh (
                script: "${command}",
                returnStdout: true
            ).trim()
            return ocCommandOutput
        }

        def notifyBuild(String buildStatus, err) {

            // In theory, null means success. In practise if there is an error thrown elsewhere than in the triggered jobs (eg. in wokraround scripts)
            // the job fails but success will be stored in buildStatus anyway
            buildStatus =  buildStatus ?: 'SUCCESS'
            
            String mailRecipients = "${RECIPIENTS}"
            String subject = "${buildStatus}: Job '${env.JOB_NAME} [${env.BUILD_NUMBER}]'"
            String body = "${subject}\n\n(${env.BUILD_URL})"

            if(err != null) {
                body = "${body}\n\n FAILURE was ${err}"
            }

            mail body: body, subject: subject, to: mailRecipients
        }

        def cleanup(String clusterAPI, namespaceName) {
            String cleanupOutput = sh(
                returnStdout: true,
                script: """
                    oc delete namespace ${namespaceName} | true

                    oc delete operatorsource integreatly-operators -n openshift-marketplace | true

                    oc delete catalogsourceconfig installed-custom-${namespaceName} -n openshift-marketplace | true

                    oc get crds | grep -v openshift.io | grep -v opentlc.com | grep -v k8s | grep -v coreos | grep -v automationbroker | grep -v NAME | awk '{print \$1}' | xargs oc delete crds | true
                """
            )
            println cleanupOutput
        }
