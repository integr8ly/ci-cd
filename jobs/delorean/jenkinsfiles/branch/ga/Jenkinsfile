#!groovy
@Library('delorean-pipeline-library')
def ghUtils = new org.integr8ly.GitHubUtils()

import org.kohsuke.github.GHCommitState
import org.kohsuke.github.GHPullRequest
import org.kohsuke.github.GitHub
import org.kohsuke.github.GitHubBuilder

def createJiraQuery(labels) {
  String query = "project = INTLY AND status = Open"

  for (i = 0; i < labels.size(); i++) {
    def label = labels[i].replaceAll('"', "'")
    query = "${query} AND labels=${label}"
  }

  return query
}

def createJiraBody(summary, gitPrUrl, labels) {
  return """{
      "fields": {
          "project": {
              "id": "12321620",
              "key": "INTLY",
              "name": "Integreatly"
          },
          "summary": "${summary}",
          "description": "${gitPrUrl}",
          "labels": ${labels},
          "issuetype": {
              "name": "Task"
          },
          "priority": {
              "id": "3",
              "name": "Major"
          },
          "customfield_12310220": "${gitPrUrl}"
      }
  }"""
}

def ghOwner = "integr8ly"
def ghRepo = "installation"
def installationGitUrl = params.installationGitUrl ?: 'git@github.com:integr8ly/installation.git'
def sourceBranch = params.installationProductBranch
def targetBranch = 'master'
def githubSSHCredentialsID = 'jenkinsgithub'
def githubUserPassCredentialsID = 'githubjenkins'
def productName = params.productName
def sourceChanges = false
boolean runIntegrationTests = false
boolean dryRun = params.dryRun
String prUrl
final String ghCommitStatusContext = 'delorean/ga/integration-tests'
String integrationTestsGHLabel = "integration tests"
def jiraUserPass = params.jiraUserPass ?: 'integreatly-jira-bot'
def productVersionVar = params.productVersionVar
def productVersion
String[] defaultPRLabels = ["product update", productName]
def installationManifestFile = './inventories/group_vars/all/manifest.yaml'
String jiraRestApiUrl = "https://issues.jboss.org/rest/api/2"

node {
    cleanWs()
    stage('Checkout') {
        gitCheckoutRepo(installationGitUrl, sourceBranch, githubSSHCredentialsID, 'installation')
    }

    stage('Check Source vs Target') {
        dir('installation') {
            sourceChanges = gitHasSourceChanges(sourceBranch, targetBranch)
            productVersion = getProductVersionFromManifest(productVersionVar, installationManifestFile)
        }
    }

    stage('Create PR') {
        when(sourceChanges) {
            println("Creating PR for ${productName}:${productVersion}")
            withCredentials([usernamePassword(
                    credentialsId: githubUserPassCredentialsID,
                    passwordVariable: 'GITHUB_PASSWORD',
                    usernameVariable: 'GITHUB_USERNAME')]) {
                final GitHub gitHub = new GitHubBuilder()
                        .withOAuthToken(env.GITHUB_PASSWORD, env.GITHUB_USERNAME)
                        .build()
                String title = "[WIP] ${productName} update"
                String body = "Created automatically by jenkins"

                if (dryRun) {
                    println "Finds an open pull request with the base branch '${sourceBranch}'. If this does not exist, it creates a new pull request with the following details:\ntitle:'${title}',\nbody '${body}',\nlabels:${defaultPRLabels}"
                    prUrl = "https://github.com/example-pr-url"
                } else {
                    GHPullRequest pr = ghUtils.ghFindOrCreatePullRequest(gitHub.getRepository("${ghOwner}/${ghRepo}"), "${ghOwner}:${sourceBranch}", targetBranch, title, body, defaultPRLabels)
                    prUrl = pr.getHtmlUrl()
                    runIntegrationTests = ghUtils.ghPrHasLabel(pr, integrationTestsGHLabel)
                }
                
                println "Component Update PR = ${prUrl}, runIntegrationTests = ${runIntegrationTests}"
            }
        }
    }

    stage('Create Jira Update Issue') {
        when(sourceChanges) {
            String[] labels = [
              /"product-update"/,
              /"${productName}"/,
              /"${productName}:${productVersion}"/
            ]
            String summary = "Update ${productName} to ${productVersion}"

            String query = createJiraQuery(labels)
            def body = createJiraBody(summary, prUrl, labels)

            def jiraID = jiraCreateIssue(jiraUserPass, jiraRestApiUrl, query, body)
            println "[INFO] Issue created: https://issues.jboss.org/browse/${jiraID}"
        }
    }

    stage('Create Jira Upgrade Issue') {
        when(sourceChanges) {
            dir('installation') {
                sh "git checkout ${targetBranch}"
                currentProductVersion = getProductVersionFromManifest(productVersionVar, installationManifestFile)
            }

            String[] labels = [
              /"product-upgrade"/,
              /"${productName}"/,
              /"${productName}:${productVersion}"/
            ]
            String summary = "Add upgrade sop or playbook for ${productName} ${currentProductVersion} to ${productVersion}"

            String query = createJiraQuery(labels)
            def body = createJiraBody(summary, prUrl, labels)
            
            def jiraID = jiraCreateIssue(jiraUserPass, jiraRestApiUrl, query, body)
            println "[INFO] Issue created: https://issues.jboss.org/browse/${jiraID}"
        }
    }

    stage('Integration Tests') {
        when(sourceChanges && runIntegrationTests) {
            withCredentials([usernamePassword(
                    credentialsId: githubUserPassCredentialsID,
                    passwordVariable: 'GITHUB_PASSWORD',
                    usernameVariable: 'GITHUB_USERNAME')]) {
                final GitHub gitHub = new GitHubBuilder()
                        .withOAuthToken(env.GITHUB_PASSWORD, env.GITHUB_USERNAME)
                        .build()
                ghUtils.ghUpdatePrCommitStatus(gitHub, prUrl, GHCommitState.PENDING, env.BUILD_URL, 'Pending', ghCommitStatusContext)
            }
            try {
                def jobName = 'openshift-cluster-integreatly-test'
                def jobParams = [
                        [$class: 'StringParameterValue', name: 'installationGitUrl', value: installationGitUrl],
                        [$class: 'StringParameterValue', name: 'installationGitBranch', value: sourceBranch],
                        [$class: 'BooleanParameterValue', name: 'dryRun', value: dryRun]
                ]
                build job: jobName, parameters: jobParams
            } catch (Exception e) {
                withCredentials([usernamePassword(
                        credentialsId: githubUserPassCredentialsID,
                        passwordVariable: 'GITHUB_PASSWORD',
                        usernameVariable: 'GITHUB_USERNAME')]) {
                    final GitHub gitHub = new GitHubBuilder()
                            .withOAuthToken(env.GITHUB_PASSWORD, env.GITHUB_USERNAME)
                            .build()
                    ghUtils.ghUpdatePrCommitStatus(gitHub, prUrl, GHCommitState.FAILURE, env.BUILD_URL, 'Failure', ghCommitStatusContext)
                    error "Integration test failures, ${installationGitUrl}, ${sourceBranch}, ${e}"
                }
            }
            withCredentials([usernamePassword(
                    credentialsId: githubUserPassCredentialsID,
                    passwordVariable: 'GITHUB_PASSWORD',
                    usernameVariable: 'GITHUB_USERNAME')]) {
                final GitHub gitHub = new GitHubBuilder()
                        .withOAuthToken(env.GITHUB_PASSWORD, env.GITHUB_USERNAME)
                        .build()
                ghUtils.ghUpdatePrCommitStatus(gitHub, prUrl, GHCommitState.SUCCESS, env.BUILD_URL, 'Success', ghCommitStatusContext)
            }
        }
    }
}
