#!groovy
@Library('delorean-pipeline-library') _

import org.integr8ly.GitHubUtils
import org.integr8ly.RegistryImage

def installationGitUrl = params.installationGitUrl ?: 'git@github.com:integr8ly/installation.git'
def installationGitRef = params.installationGitRef ?: 'master'
def githubCredentialsID = params.credentialId ?: 'githubjenkins'
def githubSSHCredentialsID = params.sshCredentialId ?: 'jenkinsgithub'
def projectOrg = params.projectOrg
def projectRepo = params.projectRepo
def productName = params.productName
def rcBranch = params.installationProductBranch ?: "${productName}-rc"
def installationManifestFile = './inventories/group_vars/all/manifest.yaml'
def integreatlyMetaFile = "integreatly.${rcBranch}.yml"
def integreatlyMetaFileForceUpdate = true

def templatesGitTagVar = params.releaseTagVar ?: params.productVersionVar
def templatesForkOrg = "integr8ly"
def templatesGitUrl = "https://github.com/${projectOrg}/${projectRepo}.git"
def templatesForkGitUrl = "https://github.com/${templatesForkOrg}/${projectRepo}.git"
def templatesDir = params.templatesDir
def templatesResourcesBaseRawVar = params.templatesResourcesBaseRawVar
def templatesResourcesGitUrlVar = params.templatesResourcesGitUrlVar
def latestPreRelease

String brewContainerRegistry = "brew-pulp-docker01.web.prod.ext.phx2.redhat.com:8888"
String rhContainerRegistry = "registry.redhat.io"
String rhContainerRegistry2 = "registry.access.redhat.com"
String clusterContainerRegistryPrivate = "docker-registry.default.svc:5000"

def rhRegistries = [brewContainerRegistry, rhContainerRegistry, rhContainerRegistry2]
def productImages = []
def productPreReleaseImages = []
def registryProjectIDs = params.registryProjectIDs.replaceAll(" ", "").split(',')

currentBuild.displayName = "${currentBuild.displayName} ${productName}"

String getThreescalePrerelease(projectOrg, projectRepo, githubCredentialsID) {
    def latestRelease = ""
    def ghUtils = new org.integr8ly.GitHubUtils()
    def tags = ghUtils.ghGetRepoTags(projectOrg, projectRepo, githubCredentialsID,"")
    tags = tags.findAll { tag -> tag.ref.matches(/.*(-CR|-ER).*/) }
    if (tags) {
        latestRelease = tags.last().ref
        latestRelease = latestRelease.minus("refs/tags/")
    }
    return latestRelease
}

String getAmqOnlinePrerelease(projectOrg, projectRepo, githubSSHCredentialsID) {
    def amqOnlineGitUrl = "git@github.com:${projectOrg}/${projectRepo}.git"
    def amqOnlineMasterBranch = "amq-online-1-dev"
    def amqOnlineLatestRC = ""
    def rcFilter = "grep 'amq-online-' | grep -v 'dev'"

    dir('amq_online') {
        gitCheckoutRepo(amqOnlineGitUrl, amqOnlineMasterBranch, githubSSHCredentialsID, '.')
        amqOnlineLatestRC = sh(returnStdout: true, script: "git for-each-ref refs/remotes/origin --format='%(refname:short)' | ${rcFilter} | sort -r | head -n 1").trim()
        amqOnlineLatestRC = amqOnlineLatestRC.minus("origin/")
    }

    return amqOnlineLatestRC
}

String getFuseOnlinePrerelease(projectOrg, projectRepo, githubCredentialsID) {
    def ghUtils = new org.integr8ly.GitHubUtils()
    return ghUtils.ghGetLatestReleaseByTag(projectOrg, projectRepo, githubCredentialsID)
}

String getLatestPreRelease(projectOrg, projectRepo, productName, githubCredentialsID, githubSSHCredentialsID) {
    String result = null
    switch (productName) {
        case '3scale':
            result = getThreescalePrerelease(projectOrg, projectRepo, githubCredentialsID)
            break
        case 'amq-online':
            result = getAmqOnlinePrerelease(projectOrg, projectRepo, githubSSHCredentialsID)
            break
        case 'fuse-online':
            result = getFuseOnlinePrerelease(projectOrg, projectRepo, githubCredentialsID)
            break
    }
    return result
}

node {
    cleanWs()

    println "[INFO] productName: ${productName}, templatesGitUrl: ${templatesGitUrl}, templatesForkGitUrl: ${templatesForkGitUrl}, templatesDir: ${templatesDir}"

    stage('Fetch Installation Repo') {
        println '[INFO] Fetch Installation Repo'
        dir('installation') {
            gitCheckoutRepo(installationGitUrl, installationGitRef, githubSSHCredentialsID, '.')
            releaseConfig = readYaml file: installationManifestFile
        }
    }

    stage('Fetch Latest Pre Release (RC)') {
        latestPreRelease = getLatestPreRelease(projectOrg, projectRepo, productName, githubCredentialsID, githubSSHCredentialsID)
        if (!latestPreRelease) {
            error "[ERROR] Unable to retrieve latest pre release version!"
        }
        println "[INFO] latestPreRelease = ${latestPreRelease}"
    }

    stage('Fetch Product Templates Repo') {
        println '[INFO] Fetch Product Templates Repo'
        dir('product-templates') {
            gitCheckoutRepo(templatesGitUrl, latestPreRelease, githubSSHCredentialsID, '.')
        }
    }

    currentBuild.description = "current: ?\n latest: ${latestPreRelease}\n"

    String templatesIntlyBranch = "${latestPreRelease}-intly"
    String templatesResourcesBaseRaw = "https://raw.githubusercontent.com/${templatesForkOrg}/${projectRepo}/${templatesIntlyBranch}/${templatesDir}"

    dir('product-templates') {
        sshagent([githubSSHCredentialsID]) {
            sh 'git config --global user.name "Automatron"'
            sh 'git config --global user.email "github.eng@feedhenry.com"'
            sh "git remote add intly ${GitHubUtils.ghTransformUrl(templatesForkGitUrl, 'ssh')}"
            sh "git fetch --all"

            stage('Collect Product Images') {
                def getProductImagesParams = [
                    targetDir: templatesDir,
                    excludeFiles: ['fuse-online-upgrade.yml'], // Ignore the file fuse-online-upgrade as it contains an image with a typo (registry.redhat.io/fuse7/fuse-ignite-uprade)
                    registries: rhRegistries,
                    registryIDs: registryProjectIDs
                ]
                productImages = getProductImages(getProductImagesParams)
              
                println "[INFO] Found ${productImages.size()} product images"
                currentBuild.description = currentBuild.description + "\nImages: ${productImages.size()}\n"
            }

            stage('Collect Product Pre Release Images') {
                def targetRegistrySecret = 'intly-redhat-registry-sa'
                def targetRegistryHost = 'https://registry.redhat.io'

                def getPreReleaseImagesParams = [
                    imageUrls: productImages,
                    registryCredentials: targetRegistrySecret,
                    registryHost: targetRegistryHost
                ]
                productPreReleaseImages = getPreReleaseImages(getPreReleaseImagesParams)

                println "[INFO] Found ${productPreReleaseImages.size()} pre release product images"
                currentBuild.description = currentBuild.description + "\nPre Release Images: ${productPreReleaseImages.size()}\n"
            }

            stage('Ensure Templates Intly Branch') {
                println("[INFO] Ensure Templates Intly Branch: ${templatesIntlyBranch}")
                gitCreateAndCheckoutBranch(templatesIntlyBranch, true, true, 'intly')
                //Currently on amq online use branches *only*, so rebase will fail, and should, when it's a tag
                def isBranch = (sh(script: "git show-ref --verify refs/heads/${latestPreRelease}", returnStatus: true) == 0)
                if (isBranch) {
                    try {
                        sh "git rebase origin/${latestPreRelease}"
                    } catch (Exception e) {
                        sh "git rebase --abort"
                    }
                }
                currentBuild.description = currentBuild.description + "\nTemplates: ${GitHubUtils.ghTransformUrl(templatesForkGitUrl, 'https', true)}/tree/${templatesIntlyBranch}\n"
            }

            stage('Update Product Image Registry') {
                gitCommitWhenChanges("Updated pre release image references to use internal cluster registry") { msgs ->
                    dir(templatesDir) {
                        productPreReleaseImages.each { imageUrl ->
                            String clusterImage = "${clusterContainerRegistryPrivate}${new URI("docker://${imageUrl}").getPath()}"
                            println("rhImage: ${imageUrl}, clusterImage: ${clusterImage}")
                            String rhImageUrlEscaped = imageUrl.replace("/", "\\/")
                            String clusterImageUrlEscaped = clusterImage.replace("/", "\\/")
                            sh "find . -name '*.y*ml' -exec sed -i -e 's/${rhImageUrlEscaped}/${clusterImageUrlEscaped}/g' {} \\;"
                        }
                    }
                    sh "git diff"
                }
            }

            stage('Push Templates Intly Branch') {
                boolean doPush = templatesIntlyBranch != 'master'
                if (doPush) {
                    sh "git push intly ${templatesIntlyBranch} --force"
                }
            }
        }
    }

    dir('installation') {
        sshagent([githubSSHCredentialsID]) {
            sh 'git config --global user.name "Automatron"'
            sh 'git config --global user.email "github.eng@feedhenry.com"'

            stage('Ensure Product RC Branch') {
                gitCreateAndCheckoutBranch(rcBranch, true, true)
                gitRebaseBranch(installationGitRef, rcBranch, false)
                currentBuild.description = currentBuild.description + "\nInstallation: ${GitHubUtils.ghTransformUrl(installationGitUrl, 'https', true)}/tree/${rcBranch}\n"
            }

            stage('Product Version Update') {
                gitCommitWhenChanges("Updated ${productName} product version to ${latestPreRelease}") { msgs ->
                    manifestFileTxt = readFile(installationManifestFile)
                    manifestFileTxt = updateManifestVariable(manifestFileTxt, templatesGitTagVar, templatesIntlyBranch)
                    manifestFileTxt = updateManifestVariable(manifestFileTxt, templatesResourcesBaseRawVar, templatesResourcesBaseRaw)
                    manifestFileTxt = updateManifestVariable(manifestFileTxt, templatesResourcesGitUrlVar, templatesForkGitUrl)
                    writeFile file: installationManifestFile, text: manifestFileTxt
                }
            }

            stage('Image Sync Meta Update') {
                gitCommitWhenChanges("Updated imageSync options for ${latestPreRelease}") { msgs ->
                    def metaConfig = [imageSync: [sourceRepo: brewContainerRegistry, projects: [:]]]
                    if (fileExists(integreatlyMetaFile)) {
                        if (!integreatlyMetaFileForceUpdate) {
                            metaConfig = readYaml file: integreatlyMetaFile
                        }
                        //Delete the file here since 'writeYaml' will fail if the file already exists
                        sh "rm ${integreatlyMetaFile}"
                    }
                    productPreReleaseImages.each { imageUrl ->
                        URI uri = new URI("docker://${imageUrl}");
                        def pathParts = uri.getPath().split(/\//) - null - ''
                        def project = pathParts[0]
                        def image = pathParts[1]
                        if (!metaConfig['imageSync']['projects'].containsKey(project)) {
                            metaConfig['imageSync']['projects'][project] = [images: []]
                        }
                        metaConfig['imageSync']['projects'][project]['images'] << image
                    }
                    writeYaml file: integreatlyMetaFile, data: metaConfig
                    sh "git add ${integreatlyMetaFile}"
                    sh "git status"
                }
            }

            stage('Push Product RC Branch') {
                boolean doPush = rcBranch != 'master'
                if (doPush) {
                    gitPushBranch(rcBranch, true)
                }
            }
        }
    }
}
